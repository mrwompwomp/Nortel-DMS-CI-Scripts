COMMAND DLOG_COLLECT(
	IF ((RINDEX^=2) & (RINDEX^=3))THEN(
		PRINT 'SYNTAX: DLOG_COLLECT Starttime Endtime';
		PRINT 'DLOG_COLLECT LAST n [MINS, HOURS, DAYS]';
		EXIT;
	)
	'MINS'->MINS;'HOURS'->HOURS;'DAYS'->DAYS;
	IF (RINDEX=2)THEN(
		@1->ST;
		@2->ET;
		FALSE->LF;
	)ELSE(
		@2->$INTVAL;
		@3->$TIMEUNIT;
		TRUE->LF;
	)
	ERASE MINS HOURS DAYS;
	READ CMD_DLOG_COLLECT;
)

CMD_DLOG_COLLECT:
 
SEND SINK;
ERASESF CURDT;
SEND SFDEV CURDT;DATE;SEND PREVIOUS;
LISTSF;
EDIT CURDT;
DOWN;LINESTR->$DATESTR;
QUIT;
ERASESF CURDT;
(DECSTRTONUM (SUBSTR $DATESTR 31 2))->I;
(DECSTRTONUM (SUBSTR $DATESTR 28 2))->H;
(DECSTRTONUM (SUBSTR $DATESTR 15 2))->D;
(SUBSTR $DATESTR 18 3)->M;
IF (M='JAN')THEN('01'->M;)
IF (M='FEB')THEN('02'->M;)
IF (M='MAR')THEN('03'->M;)
IF (M='APR')THEN('04'->M;)
IF (M='MAY')THEN('05'->M;)
IF (M='JUN')THEN('06'->M;)
IF (M='JUL')THEN('07'->M;)
IF (M='AUG')THEN('08'->M;)
IF (M='SEP')THEN('09'->M;)
IF (M='OCT')THEN('10'->M;)
IF (M='NOV')THEN('11'->M;)
IF (M='DEC')THEN('12'->M;)
(SUBSTR $DATESTR 22 4)->Y;
I+(H*60)+(D*1440)->T;
IF ($TIMEUNIT='HOURS')THEN($INTVAL*60->$INTVAL;)
IF ($TIMEUNIT='DAYS')THEN($INTVAL*1440->$INTVAL;)
T-$INTVAL->T;
IF (LF)THEN(
	Y+M+(NUMTODECSTR D 2)+(NUMTODECSTR H 2)+(NUMTODECSTR I 2)+'00'->ET;
	IF (T<=0)THEN(
		Y+M+'01000000'->ST;
		SEND PREVIOUS;PRINT 'Logs truncated at '+ST;SEND SINK;
	)ELSE(
		T/1440->D;
		T-(D*1440)->T;
		T/60->H;
		T-(H*60)->I;
		Y+M+(NUMTODECSTR D 2)+(NUMTODECSTR H 2)+(NUMTODECSTR I 2)+'00'->ST;
))
ERASE M $DATESTR I H Y D T $TIMEUNIT $INTVAL;
SEND SFDEV;DLOG;SEND PREVIOUS;
LISTSF;
EDIT CONSOLE;
END;
LINE->C;
QUIT;
SEND PREVIOUS;
IF (C=2)THEN(
	PRINT 'DLOG utility unavailable.';
)ELSE(
	PRINT 'Entered DLOG, attempting format...';
)
SEND SINK;
ERASESF CONSOLE;
INCLUDEALL;INCLUDEPROTO;
SEND SFDEV;FORMAT ST ET ToFile SFDEV;SEND PREVIOUS;
QUIT;
LISTSF;
EDIT CONSOLE;
END;
IF (LINE<3)THEN(
	SEND PREVIOUS;PRINT CONSOLE;SEND SINK;
)ELSE(
	TOP;DOWN 'adjusted';
	IF (LINE^=0)THEN(SEND PREVIOUS;PRINT 'Warning: '+LINESTR;SEND SINK;)
	DOWN 'Final formatted';
	DOWN 'FORMAT -- Failed';
	IF (LINE^=0)THEN(SEND PREVIOUS;PRINT LINESTR;SEND SINK;))
ERASE C ST ET LF;
QUIT ALL;
SEND PREVIOUS;

