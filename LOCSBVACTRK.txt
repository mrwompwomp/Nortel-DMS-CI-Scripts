%Script to print SYSB Vacant trunks - Frederic Desautels, 2023
SEND SINK;ERASESF CONSOLE;ERASESF TRKQRRES;
QUIT ALL;MAPCI NODISP;MTC;TRKS;CARRIER;
SEND SFDEV;DISP SYSB;SEND PREVIOUS;
QUIT ALL;LISTSF;EDIT CONSOLE;
END;LINE->C;line 1;
IF (C<5) THEN (SEND PREVIOUS;PRINT 'No SYSB trunks';SEND SINK;)
SEND SFDEV TRKQRRES;
REPEAT (C-3) (DOWN;
0->D;
REPEAT 4 (
(SUBSTR (LINESTR) (17*D) 12)->B;
(SUBSTR B 0 3)->PMTY;
IF ((PMTY^='SMU') & (PMTY^='   ')) THEN (
IF ((SUBSTR B 3 1) ^= ' ') THEN (4->PMLEN;) ELSE (3->PMLEN;)
(DECSTRTONUM (SUBSTR B 5 3))->PMNO;
(DECSTRTONUM (SUBSTR B 9 3))->CKT;
TRKQUERY PM (STRTOSYM (SUBSTR B 0 PMLEN)) PMNO CKT;
)
D+1->D;
))
SEND PREVIOUS;
QUIT;
FALSE->VF;
LISTSF;EDIT TRKQRRES;
WHILE (DOWN 'Location') (
DOWN 26;
IF ((SUBSTR (LINESTR) 0 10) = 'Vacant    ') THEN (
UP 2;
SEND PREVIOUS;PRINT (SUBSTR (LINESTR) 0 12)+' Vacant';SEND SINK;
TRUE->VF;
))
QUIT;
ERASESF TRKQRRES;ERASESF CONSOLE;
ERASE B D CLLI CKT PMNO PMLEN PMTY;
SEND PREVIOUS;
IF ((^VF) & (C>5)) THEN (PRINT 'No Vacant trunks';)
ERASE VF C;
